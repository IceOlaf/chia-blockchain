(mod ((ROYALTY_ADDRESS TRADE_PRICE_PERCENTAGE URI SETTLEMENT_MOD_HASH CAT_MOD_HASH) current_owner new_owner trade_price_list my_nft_id solution)

    (include condition_codes.clvm)
    (include curry-and-treehash.clinc)
    (defconstant HUNDRED 100)

    ; trade_price_list is a list of (amount type) pairs
    ; type 0 is standard chia
    ; any other type is a bytes32 mod hash of a launcher

    (defun-inline cat_settlement_puzzle_hash (CAT_MOD_HASH tail_hash SETTLEMENT_MOD_HASH)
      (puzzle-hash-of-curried-function CAT_MOD_HASH
                                       SETTLEMENT_MOD_HASH
                                       (sha256 ONE tail_hash)
                                       (sha256 ONE CAT_MOD_HASH)
      )
    )

    (defun-inline calculate_percentage (amount percentage)
      (f (divmod (* amount percentage) HUNDRED))
    )

    (defun sha256tree1 (TREE)
          (if (l TREE)
              (sha256 2 (sha256tree1 (f TREE)) (sha256tree1 (r TREE)))
              (sha256 ONE TREE)
          )
    )

    (defun extract_trade_price_list (ROYALTY_ADDRESS TRADE_PRICE_PERCENTAGE SETTLEMENT_MOD_HASH CAT_MOD_HASH trade_price_list my_nft_id)
      (if trade_price_list
        (c
          (if (r (f trade_price_list))
              (list ASSERT_PUZZLE_ANNOUNCEMENT (sha256 (cat_settlement_puzzle_hash CAT_MOD_HASH (f (r (f trade_price_list))) SETTLEMENT_MOD_HASH) (sha256tree1 (c my_nft_id (list (list ROYALTY_ADDRESS (calculate_percentage (f (f trade_price_list)) TRADE_PRICE_PERCENTAGE)))))))
              (list CREATE_COIN ROYALTY_ADDRESS (calculate_percentage (f (f trade_price_list)) TRADE_PRICE_PERCENTAGE))
          )
          (extract_trade_price_list ROYALTY_ADDRESS TRADE_PRICE_PERCENTAGE SETTLEMENT_MOD_HASH CAT_MOD_HASH (r trade_price_list) my_nft_id)
        )
        ()
      )
    )

    ; main
    (r (c URI (extract_trade_price_list ROYALTY_ADDRESS TRADE_PRICE_PERCENTAGE SETTLEMENT_MOD_HASH CAT_MOD_HASH trade_price_list my_nft_id)))
)
